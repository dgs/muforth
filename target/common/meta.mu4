( This file is part of muFORTH: http://pages.nimblemachines.com/muforth

  Copyright 2002-2008 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)

( meta.mu4 - bits common to all metacompiler targets)

( daf 2001-feb-02. Created, from v25 version.)
( daf 2002-apr-01. Converted to muforth.)

" Meta compiler common "  file[#

forth  decimal

( Metacompiler vocabs)
chain .machine.   ( words specific to a target machine)
chain .outside.   ( used outside of target definitions - the `meta' forth)
chain .inside.    ( used inside target definitions - the `meta' compiler)
chain .definer.   ( used inside definer definitions - the `meta' definer)
chain .assembler. ( the host-resident target assembler)
chain .target.    ( the target words - like `forth' on the host)

: machine      .machine. definitions ;
: outside      .outside. definitions ;
: inside        .inside. definitions ;
: assembler  .assembler. definitions ;
: definer      .definer. definitions ;
: target        .target. definitions ;

compiler
: \m   ( compile from machine)      .machine. \chain ;
: \o   ( compile from outside)      .outside. \chain ;
: \i   ( compile from inside)        .inside. \chain ;
: \a   ( compile from assembler)  .assembler. \chain ;
: \d   ( compile from definer)      .definer. \chain ;
: \t   ( compile from target)        .target. \chain ;


( XXX)
forth
defer target-remote
defer target-compile,

-: error" We're not connected to the target yet." ;  is target-remote

( These are universal!)

( For target-compiling.)
-:  ."  (outside)"  ;
-:    .outside. find  if  execute ^  then  number ;
mode __outside__

( Inside a target definition.)
-:  ."  (inside)"  ;
-:     .inside. find  if  execute ^  then
       .target. find  if  execute target-remote ^  then  number ;
mode __inside__

( For interacting with a tethered target.)
-:  ."  (chatting)"  ;
-:     .target. find  if  execute target-remote ^  then
      .outside. find  if  execute ^         then  number ;
mode __chat__

-:  ."  (assembling)"  ;
-:    .machine. find  if  execute ^  then
    .assembler. find  if  execute ^  then  number ;
mode __asm__

-:  ."  (assembling a macro)"  ;
-:   .compiler. find  if  execute ^   then
      .machine. find  if  compile, ^  then
    .assembler. find  if  compile, ^  then  number literal ;
mode __macro__

forth
: \   .outside. chain' execute ;  ( escape to meta - find and execute a word)

outside
: \     .forth. chain' execute ;  ( escape to forth - find and execute a word)


( Random useful goodies.)
: hex      hex  ;
: decimal  decimal  ;
: octal    octal  ;
: .     .  ;
: u.    u.  ;
: .s    .s  ;

: comment  comment ;
: (     \f (  ;
: char  \f char  ;
: ."    \f ."  ;
: "     \f "  ;

: cr    cr  ;
: file[   file[  ;
: ]file   ]file  ;
: ld     ld  ;

: __asm__   __asm__ ;
: __forth__   \c [ ;
forth

#]file
